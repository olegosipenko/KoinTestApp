// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: "com.github.ben-manes.versions"

buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath GradlePlugins.androidGradle
    classpath GradlePlugins.kotlin
    classpath GradlePlugins.detekt
    classpath GradlePlugins.depUpdate
    classpath GradlePlugins.hilt
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
  configurations.all { // https://github.com/mockk/mockk/issues/281
    resolutionStrategy {
      force("org.objenesis:objenesis:2.6")
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

dependencyUpdates {
  gradleReleaseChannel = "current"

  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'BETA']
      .any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }

  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}